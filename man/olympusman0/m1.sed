:x
s/``/`/g
s/''/'/g
s/\\\*-/\\-/g
s/\\\*a/\\'/g
s/\\\*g/\\`/g
s/\\\*_/_/g
s/\\\*v/\\(bv/g
s/\\\*X/\\(mu/g
s/\\\*>/\\(->/g
s/\\\*b/\\(*b/g
s/\\\*p/\\(*p/g
s/\\\*G/\\(*G/g
s/\\\*r/\\*R/g
s/\\\*u/\\(*m/g
/^\.li/{
	N
	p
	d
}
s/^\.dt/.DT/
s/^\.th/.TH/
s/^\.sh/.SH/
s/^\.it/.I/
s/^\.bd/.B/
s/^\.Bd/.bd/
/^\.lp/{
	s/.lp/.br\
.ns\
.Lp/
	P
	D
}
/^\.s[123]/{
	N
	/\n\.i0/s/.*/.PP/
	/\n\.lp/s/.*\n//
	/s[123]/{
		s//DP/
		P
		D
	}
}
/^\.i0/{
	N
	/\.s[123]/s/.*/.PP/
	s/.i0.*\n//
}
s/^\.Lp/.lp/
/^\.lp/{
	/ *0 *$/{
		s///
		s/lp *+/IP "" /
		p
		d
	}
	s/ *+/ /
	s/ *[^ ]* *$//
	N
	/	/!{
		s/lp/HP/
	}
	s/^.lp/.TP/
	s/	/\
/
	P
	D
}
/^\.I *[^ ]*$/{
	N
	s/^.I\(.*\)\n\(([IVX][ALMIVXSZ]*)[^ ]*\) */.IR \1 \2\
/
	s/\n$//
	P
	D
}
s/ \(([IVX][ALMIVXSZ]*)\)/\1/g
/^\\f/{
	s/\\f/@/g
	s/@P/@R/g
	s/^@\(.\)\([^@\n]*\)@\(.\)\([^@\n]*\)@\1\([^\n]*\)/.\1\3 "\2" "\4" "\5"/
	s/^@\(.\)\([^@\n]*\)@\(.\)\([^\n]*\)/.\1\3 "\2" "\4"/
	s/^@\(.\)\([^\n]*\)/.\1 "\2"/
	s/@R"$/"/
	s/@R"\n/"\
/
	s/@/\\f/g
}
P
D
