(*****************************************************************************)
(*                                                                           *)
(*                             File: CODE.5.TEXT                             *)
(*                                                                           *)
(*           (C) Copyright 1980, 1985 Silicon Valley Software, Inc.          *)
(*                                                                           *)
(*                            All rights reserved.               04-Oct-85   *)
(*                                                                           *)
(*****************************************************************************)


  procedure expr2(fop: integer);
    var lcattr,tcattr: cattr; lop,mop,lsize,msize,bits: integer;
        lcc: condition; d,d2,a,a1,a2: pregrec; QuickFlag,DoIt: Boolean;
  begin
  case fop of
   64, { IADD2 }
   65, { IADD4 }
   66, { ISUB2 }
   67: { ISUB4 }
       begin
       if odd(fop)
       then lsize := LONG
       else lsize := WORD;
       numexpr(nextbyte,lsize); lcattr := gcattr; numexpr(nextbyte,lsize);
       make2valid(gcattr,lcattr);
       if fop <= 65
       then begin
            lop := -12224; { ADD.W }
            if lcattr.ckind = CNST
            then begin
                 tcattr := lcattr; lcattr := gcattr; gcattr := tcattr;
                 end;
            end
       else begin
            lop := -28608; { SUB.W }
            if (gcattr.ckind = STCK) and (lcattr.ckind = STCK)
            then d := loadd(gcattr,lsize);
            lockcattr(gcattr); d := loadd(lcattr,lsize); unlockcattr(gcattr);
            end;
       if odd(fop) then lop := lop + 64; { .W --> .L }
       QuickFlag := FALSE;
       if gcattr.ckind = CNST
       then if abs(gcattr.cvalu.lvalu) <= 8
            then begin
                 QuickFlag := TRUE;
                 if gcattr.cvalu.lvalu <> 0
                 then begin
                      d := loadd(lcattr,lsize); isolate(d);
                      if fop >= 66
                      then gcattr.cvalu.lvalu := -gcattr.cvalu.lvalu;
                      addidreg(d,gcattr.cvalu.lvalu,lsize);
                      gcattr.ckind := EXPR; gcattr.exreg := d;
                      end
                 else gcattr := lcattr;
                 end;
       if not QuickFlag
       then begin
            if lcattr.ckind = EXPR
            then begin
                 lockcattr(gcattr); d := loadd(lcattr,lsize); isolate(d);
                 unlockcattr(gcattr); lcattr := gcattr;
                 end
            else begin
                 lockcattr(lcattr); d := loadd(gcattr,lsize); isolate(d);
                 unlockcattr(lcattr);
                 end;
            lock(d);
            geneffaddr(lop + d^.regno*512,lcattr,lsize,FALSE,0);
            unlock(d); ccodereg := d^.regno; erase(d);
            gcattr.ckind := EXPR; gcattr.exreg := d;
            end;
       end;
   68: { IMULT2 }
       begin
       numexpr(nextbyte,WORD); lcattr := gcattr; numexpr(nextbyte,WORD);
       if gcattr.ckind = CNST
       then begin tcattr := lcattr; lcattr := gcattr; gcattr := tcattr; end;
       make2valid(gcattr,lcattr);
       DoIt := TRUE;
       if lcattr.ckind = CNST
       then
         if ShiftMult(lcattr.cvalu.lvalu,bits)
         then begin
              DoIt := FALSE;
              if bits > 0
              then begin
                   d := loadd(gcattr,WORD); isolate(d); erase(d);
                   if bits > 8 then { LSL.W #8,D } genr(-7864,d);
                   { LSL.W #bits,D } gennr(-7864,bits,d);
                   gcattr.exreg := d; ccodereg := d^.regno;
                   end;
              end;
       if DoIt
       then begin
            if lcattr.ckind = EXPR
            then if not reg[lcattr.exreg^.regno].Perm
                 then begin
                      tcattr := lcattr; lcattr := gcattr; gcattr := tcattr;
                      end;
            lockcattr(lcattr); d := loadd(gcattr,WORD); isolate(d);
            unlockcattr(lcattr); lock(d); erase(d);
            geneffaddr(-15936 + d^.regno*512,lcattr,WORD,FALSE,0);
            unlock(d);
            gcattr.ckind := EXPR; gcattr.exreg := d; ccodereg := d^.regno;
            end;
       end;
   69: { IMULT4 }
       begin
       numexpr(nextbyte,LONG); lcattr := gcattr; numexpr(nextbyte,LONG);
       if gcattr.ckind = CNST
       then begin tcattr := lcattr; lcattr := gcattr; gcattr := tcattr; end;
       DoIt := TRUE;
       if lcattr.ckind = CNST
       then if ShiftMult(lcattr.cvalu.lvalu,bits)
            then begin
                 DoIt := FALSE;
                 if bits > 0
                 then begin
                      d := loadd(gcattr,LONG); isolate(d); gcattr.exreg := d;
                      if bits > 8 then { LSL.L #8,D } genr(-7800,d);
                      { LSL.L #bits,D } gennr(-7800,bits,d);
                      erase(d);
                      end;
                 end;
       if DoIt
       then if FPHardware and MAX_FLAG
            then max2cattr(fop,lcattr,gcattr)
            else begin
                 pushvalue(lcattr,LONG); pushvalue(gcattr,LONG);
                 genjsra('%I_MUL4 ');
                 gcattr.ckind := STCK;
                 end;
       end;
   70, { IDIV2 }
   72: { IMOD2 }
       begin
       numexpr(nextbyte,WORD); lcattr := gcattr; numexpr(nextbyte,WORD);
       make2valid(gcattr,lcattr);
       if (gcattr.ckind = STCK) and (lcattr.ckind = STCK)
       then begin
            lockcattr(lcattr); d := loadd(gcattr,WORD);
            unlockcattr(lcattr);
            end;
       lockcattr(gcattr); d := loadd(lcattr,WORD); isolate(d);
       genr(18624,d); { EXT.L } unlockcattr(gcattr); lock(d);
       geneffaddr(-32320 + d^.regno*512,gcattr,WORD,FALSE,0);
       unlock(d); ccodereg := d^.regno; erase(d);
       if fop = 72 then genr(18496,d); { SWAP }
       gcattr.ckind := EXPR; gcattr.exreg := d;
       end;
   71: { IDIV4 } if FPHardware and MAX_FLAG
                 then max2expr(71)
                 else push44call('%I_DIV4 ',TRUE,4);
   73: { IMOD4 } if FPHardware and MAX_FLAG
                 then max2expr(73)
                 else push44call('%I_MOD4 ',TRUE,4);
   74, { INEG2 }
   75, { INEG4 }
   76, { IABS2 }
   77: { IABS4 }
       begin
       if odd(fop)
       then begin lop := 17536; { NEG.L } lsize := LONG; end
       else begin lop := 17472; { NEG.W } lsize := WORD; end;
       expression(nextbyte); d := loadd(gcattr,lsize); isol(d,lsize,TRUE);
       if fop > 75
       then begin
            if ccodereg <> d^.regno then testd(d,lsize);
            { BPL $+4 } gen(27138);
            end;
       genr(lop,d); gcattr.exreg := d; ccodereg := d^.regno; erase(d);
       end;
   78: { I2**I2 }
       begin
       expression(nextbyte); lcattr := gcattr; expression(nextbyte);
       if (gcattr.ckind = CNST) and (gcattr.cvalu.lvalu = 2)
       then begin
            d := loadd(lcattr,WORD); isolate(d); erase(d);
            genrr(-15936,d,d);
            gcattr.ckind := EXPR; gcattr.exreg := d; ccodereg := d^.regno;
            end
       else begin
            d := loadd(lcattr,WORD); isolate(d);
            { EXT.L D } genr(18624,d);
            { MOVE.L D,-(SP) } genr(12032,d);
            freereg(d);
            d := loadd(gcattr,WORD); isolate(d);
            { EXT.L D } genr(18624,d);
            { MOVE.L D,-(SP) } genr(12032,d);
            freereg(d);
            saveallregs; genjsra('%I_UP_I '); clearscr;
            gcattr.ckind := EXPR; gcattr.exreg := getreg(D0,D0);
            end;
       end;
   79: { I4**I4 }
       begin
       expression(nextbyte); pushvalue(gcattr,LONG); expression(nextbyte);
       if (gcattr.ckind = CNST) and (gcattr.cvalu.lvalu = 2)
       then begin
            { MOVE.L (SP),-(SP) } gen(12055);
            if FPHardware and MAX_FLAG
            then maxcmd(M_IMUL4,4)
            else genjsra('%I_MUL4 ');
            gcattr.ckind := STCK;
            end
       else begin
            pushvalue(gcattr,LONG);
            saveallregs; genjsra('%I_UP_I '); clearscr;
            gcattr.ckind := EXPR; gcattr.exreg := getreg(D0,D0);
            end;
       end;
   80, { AND1 }
   81, { AND2 }
   82, { AND4 }
   83, { IOR1 }
   84, { IOR2 }
   85: { IOR4 }
       begin
       if fop > 82
       then lop := -32767 - 1 { OR }
       else lop := -16384; { AND }
       mop := (fop - 80) mod 3;
       if mop = 0
       then lsize := BYTE
       else if mop = 1
            then begin lsize := WORD; lop := lop + 64; end
            else begin lsize := LONG; lop := lop + 128; end;
       expression(nextbyte);
       if gcattr.ckind = COND then loadcc(gcattr);
       lcattr := gcattr; expression(nextbyte);
       if gcattr.ckind = COND then loadcc(gcattr);
       if IsAReg(lcattr) then d := loadd(lcattr,lsize);
       if IsAReg(gcattr) then d := loadd(gcattr,lsize);
       make2valid(gcattr,lcattr);
       if (lcattr.ckind = EXPR) or
          (lcattr.ckind = BOOL) or
          (lcattr.ckind = BITZ)
       then begin
            d := lcattr.exreg; tcattr := gcattr;
            end
       else begin
            if (gcattr.ckind = BOOL) or (gcattr.ckind = BITZ)
            then d := gcattr.exreg
            else begin
                 lockcattr(lcattr); d := loadd(gcattr,lsize);
                 unlockcattr(lcattr);
                 end;
            tcattr := lcattr;
            end;
       if (tcattr.ckind = BOOL) or (tcattr.ckind = BITZ)
       then tcattr.ckind := EXPR;
       isolate(d); erase(d); lock(d);
       geneffaddr(lop + d^.regno*512,tcattr,lsize,FALSE,0);
       unlock(d);
       if fop <= 82
       then {--> AND | BOOL  BITZ  else }
              { -----+----------------- }
              { BOOL | BOOL  BITZ  EXPR }
              { BITZ | BITZ  BITZ  EXPR }
              { else | EXPR  EXPR  EXPR }
            
            begin
            if ((gcattr.ckind = BOOL) or (gcattr.ckind = BITZ)) and
               ((lcattr.ckind = BOOL) or (lcattr.ckind = BITZ))
            then if gcattr.ckind = BOOL
                 then gcattr.ckind := lcattr.ckind
                 else gcattr.ckind := BITZ
            else gcattr.ckind := EXPR;
            end
       else {--> OR  | BOOL  BITZ  else }
              { -----+----------------- }
              { BOOL | BOOL  BITZ  BITZ }
              { BITZ | BITZ  BITZ  BITZ }
              { else | BITZ  BITZ  EXPR }
            
            if (gcattr.ckind = BOOL) and (lcattr.ckind = BOOL)
            then gcattr.ckind := BOOL
            else if ((gcattr.ckind <> BOOL) and (gcattr.ckind <> BITZ)) and
                    ((lcattr.ckind <> BOOL) and (lcattr.ckind <> BITZ))
                 then gcattr.ckind := EXPR
                 else gcattr.ckind := BITZ;
       if gcattr.ckind = EXPR then ccodereg := d^.regno;
       gcattr.exreg := d;
       end;
   86, { XOR1 }
   87, { XOR2 }
   88: { XOR4 }
       begin
       lop := fop - 86;
       if lop = 0
       then begin lsize := BYTE; lop := -20224; { EOR.B } end
       else if lop = 1
            then begin lsize := WORD; lop := -20160; { EOR.W } end
            else begin lsize := LONG; lop := -20096; { EOR.L } end;
       expression(nextbyte); lcattr := gcattr; expression(nextbyte);
       makevalid(gcattr); d := loadd(gcattr,lsize); isolate(d); lock(d);
       makevalid(lcattr); d2 := loadd(lcattr,lsize); unlock(d); erase(d);
       genrr(lop,d2,d); freereg(d2); gcattr.exreg := d; ccodereg := d^.regno;
       end;
   89, { NOT1 }
   90, { NOT2 }
   91: { NOT4 }
       begin
       lop := 17920; { NOT }
       fop := fop - 89;
       if fop = 0
       then lsize := BYTE
       else if fop = 1
            then begin lsize := WORD; lop := lop + 64; end
            else begin lsize := LONG; lop := lop + 128; end;
       expression(nextbyte);
       d := loadd(gcattr,lsize); isolate(d); erase(d); genr(lop,d);
       gcattr.ckind := EXPR; gcattr.exreg := d; ccodereg := d^.regno;
       end;
   92, { LT1 }
   93, { LT2 }
   94, { LT4 }
   95, { GT1 }
   96, { GT2 }
   97, { GT4 }
   98, { LE1 }
   99, { LE2 }
  100, { LE4 }
  101, { GE1 }
  102, { GE2 }
  103, { GE4 }
  104, { EQ1 }
  105, { EQ2 }
  106, { EQ4 }
  107, { NE1 }
  108, { NE2 }
  109: { NE4 }
       begin
       fop := fop - 92;
       case fop mod 3 of
         0: lsize := BYTE;
         1: lsize := WORD;
         2: lsize := LONG;
       end; {case}
       case fop div 3 of
         0: lcc := LT;
         1: lcc := GT;
         2: lcc := LE;
         3: lcc := GE;
         4: lcc := EQ;
         5: lcc := NE;
       end; {case}
       numexpr(nextbyte,lsize); lcattr := gcattr; numexpr(nextbyte,lsize);
       if GenCompare(lcattr,gcattr,lsize)
       then case lcc of
             LT: lcc := GT;
             LE: lcc := GE;
             GT: lcc := LT;
             GE: lcc := LE;
            end; {case}
       gcattr.ckind := COND; gcattr.cc := lcc;
       end;
  110: { BNOT }
       begin
       expression(nextbyte);
       with gcattr do
            if ckind = COND
            then if odd(ord(cc))
                 then cc := pred(cc)
                 else cc := succ(cc)
            else if (ckind = BOOL) or (ckind = BITZ)
                 then begin
                      makevalid(gcattr); isolate(exreg); erase(exreg);
                      { ADDQ.B #1,D } genr(20992,exreg);
                      if ckind = BOOL then ckind := EXPR;
                      end
                 else begin
                      d := loadd(gcattr,BYTE); isolate(d); erase(d);
                      { EORI.B #1,d } genr(2560,d); gen(1);
                      gcattr.exreg := d; ccodereg := d^.regno;
                      end;
       end;
  111: { ODD }
       begin
       expression(nextbyte); d := loadd(gcattr,BYTE); isolate(d); erase(d);
       { ANDI.W #1,D } genr(576,d); gen(1);
       gcattr.exreg := d; ccodereg := d^.regno;
       end;
  112: { FADD4 } if FPHardware
                 then if SKY_FLAG
                      then sky44(S_FADD)
                      else if MAX_FLAG
                           then max2expr(112)
                           else hit2expr(112)
                 else push44call('%F_ADD  ',TRUE,4);
  113: { FADD8 } if FPHardware and MAX_FLAG
                 then max2expr(113)
                 else push88call('%D_ADD  ',TRUE,8);
  114: { FSUB4 } if FPHardware
                 then if SKY_FLAG
                      then sky44(S_FSUB)
                      else if MAX_FLAG
                           then max2expr(114)
                           else hit2expr(114)
                 else push44call('%F_SUB  ',TRUE,4);
  115: { FSUB8 } if FPHardware and MAX_FLAG
                 then max2expr(115)
                 else push88call('%D_SUB  ',TRUE,8);
  116: { FMUL4 } if FPHardware
                 then if SKY_FLAG
                      then sky44(S_FMUL)
                      else if MAX_FLAG
                           then max2expr(116)
                           else hit2expr(116)
                 else push44call('%F_MUL  ',TRUE,4);
  117: { FMUL8 } if FPHardware and MAX_FLAG
                 then max2expr(117)
                 else push88call('%D_MUL  ',TRUE,8);
  118: { FDIV4 } if FPHardware and MAX_FLAG
                 then max2expr(118)
                 else push44call('%F_DIV  ',TRUE,4);
  119: { FDIV8 } if FPHardware and MAX_FLAG
                 then max2expr(119)
                 else push88call('%D_DIV  ',TRUE,8);
  122, { FLT4 }
  123, { FLT8 }
  124, { FGT4 }
  125, { FGT8 }
  126, { FLE4 }
  127, { FLE8 }
  128, { FGE4 }
  129, { FGE8 }
  130, { FEQ4 }
  131, { FEQ8 }
  132, { FNE4 }
  133: { FNE8 }
       begin
       if FPHardware and MAX_FLAG
       then max2expr(fop)
       else if FPHardware and HIT_FLAG and not odd(fop)
            then hit2expr(fop)
       else begin
            if odd(fop)
                 then push88call('%D_CMP  ',FALSE,0)
                 else push44call('%F_CMP  ',FALSE,0);
            with gcattr do begin
                 ckind := COND;
                 case fop of
                      122,123: { FLT4/8 } cc := LT;
                      124,125: { FGT4/8 } cc := GT;
                      126,127: { FLE4/8 } cc := LE;
                      128,129: { FGE4/8 } cc := GE;
                      130,131: { FEQ4/8 } cc := EQ;
                      132,133: { FNE4/8 } cc := NE;
                 end; {case}
                 end;
            end;
       end;
  134, { FNEG4 }
  135, { FNEG8 }
  136, { FABS4 }
  137: { FABS8 }
       begin
       expression(nextbyte);
       if odd(fop) then pushvalue(gcattr,QUAD) else pushvalue(gcattr,LONG);
       if fop <= 135
       then { BCHG #7,(SP) } gen(2135)
       else { BCLR #7,(SP) } gen(2199);
       gen(7);
       gcattr.ckind := STCK;
       end;
  138: { F4**I4 }
       begin
       expression(nextbyte); pushvalue(gcattr,LONG); expression(nextbyte);
       if (gcattr.ckind = CNST) and
          (gcattr.cvalu.lvalu >= 1) and (gcattr.cvalu.lvalu <= 4)
       then begin
            case gcattr.cvalu.lvalu of
              1: ;
              2: begin
                 if FPHardware and MAX_FLAG
                 then maxcmd(M_FSQR,0)
                 else begin
                      { MOVE.L (SP),-(SP) } gen(12055);
                      genjsra('%F_MUL  ');
                      end;
                 end;
              3: begin
                 gen(12055);
                 if FPHardware and MAX_FLAG
                 then begin maxcmd(M_FSQR,0); maxcmd(M_FMUL,4); end
                 else begin
                      gen(12055);
                      genjsra('%F_MUL  '); genjsra('%F_MUL  ');
                      end;
                 end;
              4: begin
                 if FPHardware and MAX_FLAG
                 then begin maxcmd(M_FSQR,0); maxcmd(M_FSQR,0); end
                 else begin
                      gen(12055); genjsra('%F_MUL  ');
                      gen(12055); genjsra('%F_MUL  ');
                      end;
                 end;
            end; {case}
            gcattr.ckind := STCK;
            end
       else begin
            pushvalue(gcattr,LONG);
            saveallregs; genjsra('%_UP_I  '); clearscr;
            gcattr.ckind := EXPR; gcattr.exreg := getreg(D0,D0);
            end;
       end;
  139: { F8**I4 }
       begin
       expression(nextbyte); pushvalue(gcattr,QUAD); expression(nextbyte);
       if (gcattr.ckind = CNST) and
          (gcattr.cvalu.lvalu >= 1) and (gcattr.cvalu.lvalu <= 4)
       then begin
            case gcattr.cvalu.lvalu of
              1: ;
              2: begin
                 if FPHardware and MAX_FLAG
                 then maxcmd(M_DSQR,0)
                 else begin
                      { MOVE.L 4(SP),-(SP) } gen(12079); gen(4);
                      gen(12079); gen(4); genjsra('%D_MUL  ');
                      end;
                 end;
              3: begin
                 gen(12079); gen(4); gen(12079); gen(4);
                 if FPHardware and MAX_FLAG
                 then begin maxcmd(M_DSQR,0); maxcmd(M_DMUL,8); end
                 else begin
                      gen(12079); gen(4); gen(12079); gen(4);
                      genjsra('%D_MUL  '); genjsra('%D_MUL  ');
                      end;
                 end;
              4: begin
                 if FPHardware and MAX_FLAG
                 then begin maxcmd(M_DSQR,0); maxcmd(M_DSQR,0); end
                 else begin
                      gen(12079); gen(4); gen(12079); gen(4);
                      genjsra('%D_MUL  ');
                      gen(12079); gen(4); gen(12079); gen(4);
                      genjsra('%D_MUL  ');
                      end;
                 end;
            end; {case}
            end
       else begin
            pushvalue(gcattr,LONG);
            saveallregs; genjsra('%_DUPIV '); clearscr;
            { MOVEM.L D0/D1,-(SP) } gen(18663); gen(-16384);
            end;
       gcattr.ckind := STCK;
       end;
  143: { Transcendentals:}
       begin
       fop := nextbyte;
       if FPHardware and MAX_FLAG
       then maxtransop(fop)
       else transop(fop);
       end;
  144: { STRLT } pushaacall('%S_LT   ',FALSE,1);
  145: { STRGT } pushaacall('%S_GT   ',FALSE,1);
  146: { STRLE } pushaacall('%S_LE   ',FALSE,1);
  147: { STRGE } pushaacall('%S_GE   ',FALSE,1);
  148: { STREQ } pushaacall('%S_EQ   ',FALSE,1);
  149: { STRNE } pushaacall('%S_NE   ',FALSE,1);
  150, { PAOCLT }
  151, { PAOCGT }
  152, { PAOCLE }
  153, { PAOCGE }
  154, { PAOCEQ }
  155: { PAOCNE }
       begin
       lsize := nextbyte; msize := nextbyte;
       if lsize <> msize then error(2009);
       { !!! Special case for length=2/4 !!! }
       { !!! Special case for length=6/8 !!! but no quite as good !!! }
       expression(nextbyte); lcattr := gcattr; expression(nextbyte);
       a2 := loadaddress(gcattr); a1 := loadaddress(lcattr);
       isolate(a1); lock(a1); load(a2); isolate(a2); unlock(a1);
       erase(a1); erase(a2); d := loadint(lsize-1,WORD); isolate(d); erase(d);
       { CMPM.B (A1)+,(A2)+ } genrr(-20216,a1,a2);
       { DBNE } genr(22216,d); gen(-4);
       freereg(a1); freereg(a2); freereg(d);
       with gcattr do begin
            ckind := COND;
            case fop of
                 150: { PAOCLT } cc := LO;
                 151: { PAOCGT } cc := HI;
                 152: { PAOCLE } cc := LS;
                 153: { PAOCGE } cc := HS;
                 154: { PAOCEQ } cc := EQ;
                 155: { PAOCNE } cc := NE;
            end; {case}
            end;
       end;
  end; {case}
  end; {expr2}
  

                                                                                                                                                                                                                                                                                                                                                                                                                                          